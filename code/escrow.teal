#pragma version 4

//////////////
// opt in

// IF: Single AssetTransfer THEN: Handle Opt-In
global GroupSize
int 1
==
bnz branch_optin
b branch_payout

branch_optin:
// Opt-In has to be ASA
txn TypeEnum
int axfer
==
// Opt-In Asset Amount is 0
txn AssetAmount
int 0
==
&&
// Opt-In executed as Auto-AssetTransfer
txn Sender
txn AssetReceiver
==
&&
// Opt-In Fee limit
txn Fee
int 1000
<=
&&
// Prevent unforseen Asset Clawback
txn AssetSender
global ZeroAddress
==
&&
// Prevent Asset Close-To
txn AssetCloseTo
global ZeroAddress
==
&&
// Prevent Close-To
txn CloseRemainderTo
global ZeroAddress
==
&&
// Prevent Rekey-To
txn RekeyTo
global ZeroAddress
==
&&
bnz approve
b decline

//////////////
// TEAL to allow payout from escrow
branch_payout:

// Check that first transaction is state dapp txn
gtxn 0 ApplicationID
// TODO always replace with $STATE (whenever changed)
int 29682197
==
bnz approve
b decline

//////////////
// end branches
decline:
int 0
return

approve:
int 1
return